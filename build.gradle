buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'http://maven.blamejared.com' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
plugins {
    id 'maven'
    id 'maven-publish'
    id 'java'
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'java'

version = "$mc_version-$mod_version"

group= "matteroverdrive"
archivesBaseName = "MatterOverdrive"

static def getCurrentYear() {
    def date = new Date()
    return date.format('yyyy')
}
sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "$mc_version-$forge_version"
    runDir = "run"
    mappings = "stable_39"

    replaceIn "Reference.java"
    replace "@VERSION@", mod_version
}

repositories {
    mavenCentral()
    maven {
        name "tterrag"
        url "http://maven.tterrag.com"
    }
    maven {url "http://modmaven.k-4u.nl"}
    maven {
        name "progwml6"
        url "http://dvs1.progwml6.com/files/maven"
    }
}

dependencies {
    provided "team.chisel.ctm:CTM:MC1.12-0.2.3.12"
    provided "li.cil.oc:OpenComputers:${OC_version}"
    deobfProvided "mezz.jei:jei_${mc_version}:${jei_version}:api"
    runtime "mezz.jei:jei_${mc_version}:${jei_version}"
    compile group: 'org.projectlombok', name: 'lombok', version: '1.16.20'

    deobfCompile group: 'mcp.mobius.waila', name: 'Hwyla', version: '1.8.26-B41_1.12.2'
    deobfCompile 'CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.11.494'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    classifier = "universal"
    manifest.mainAttributes(
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"Matter Overdrive: Community Edition",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "Built-By": System.properties['user.name'],
            "Created-By": "${System.properties['java.vm.version']} (${System.properties['java.vm.vendor']})",
            'Maven-Artifact': "$group:$archivesBaseName:$version",
            "FMLAT": "mo_at.cfg"
    )
}
task apiJar(type: Jar) {
    from(sourceSets.main.output) {
        include 'matteroverdrive/api/**'
    }
    classifier = "api"
}

artifacts {
    archives apiJar
}

runClient {
    jvmArgs "-Xmx2048m", "-Xms2048m", "-ea"
    if (project.hasProperty("appArgs")) {
        args Eval.me(appArgs)
    }
}

runServer {
    jvmArgs "-Xmx1024m", "-Xms1024m", "-ea"
}
