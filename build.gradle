buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'http://maven.blamejared.com' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.selesse:gradle-git-changelog:0.2.+'
    }
}
plugins {
    id 'maven'
    id 'maven-publish'
    id 'java'
//    id 'net.minecrell.licenser' version '0.4.1'
    id "com.hrznstudio.cursegradle" version "1.1.0"
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.selesse.git.changelog'
apply plugin: 'java'

version = "$mc_version-$mod_version"

group= "matteroverdrive"
archivesBaseName = "MatterOverdrive"

static def getCurrentYear() {
    def date = new Date()
    return date.format('yyyy')
}

//license {
//    ext {
//        name = "Matter Overdrive: Community Edition"
//        organization = "Matter Overdrive: Community Edition"
//        email = "contact@hrznstudio.com"
//        url = "https://www.curseforge.com/minecraft/mc-mods/matter-overdrive-community-edition"
//        year = getCurrentYear()
//    }
//
//    header = file('HEADER')
//    include '**/*.java'
//}

changelog {
    title = "${archivesBaseName} - Changelog"

    outputDirectory = file("$projectDir")

    since = "beginning"

    commitFormat = '%s (%an)'

    markdown {
        commitFormat = '* %s (%an)'
    }

    includeLines = {
        !it.contains("Merge")
    }

    processLines = {
        String input = it as String
        if (input.contains('[ci skip] ')) {
            input = input.minus('[ci skip] ')
        }
        input
    }
}

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "$mc_version-$forge_version"
    runDir = "run"
    mappings = "stable_39"

    replaceIn "Reference.java"
    replace "@VERSION@", mod_version
}

repositories {
    mavenCentral()
    maven {
        name "tterrag"
        url "http://maven.tterrag.com"
    }
    maven {url "http://modmaven.k-4u.nl"}
    maven {
        name "progwml6"
        url "http://dvs1.progwml6.com/files/maven"
    }
}

dependencies {
    provided "team.chisel.ctm:CTM:MC1.12-0.2.3.12"
    deobfProvided "mezz.jei:jei_${mc_version}:${jei_version}:api"
    runtime "mezz.jei:jei_${mc_version}:${jei_version}"
    compile group: 'org.projectlombok', name: 'lombok', version: '1.16.20'

    deobfCompile group: 'mcp.mobius.waila', name: 'Hwyla', version: '1.8.26-B41_1.12.2'
    deobfCompile 'CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.11.494'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}


jar {
    classifier = "universal"
    manifest.mainAttributes(
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"Matter Overdrive: Community Edition",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "Built-By": System.properties['user.name'],
            "Created-By": "${System.properties['java.vm.version']} (${System.properties['java.vm.vendor']})",
            'Maven-Artifact': "$group:$archivesBaseName:$version",
            "FMLAT": "mo_at.cfg"
    )
}

task deobfJar(type: Jar) {
    from(sourceSets.main.java.srcDirs) {
        include '*'
    }
    from(sourceSets.main.resources.srcDirs) {
        include '*'
    }
    from(sourceSets.main.output) {
        exclude '*'
    }
    classifier = "deobf"
}

task apiJar(type: Jar) {
    from(sourceSets.main.output) {
        include 'matteroverdrive/api/**'
    }
    classifier = "api"
}

artifacts {
    archives deobfJar
    archives apiJar
}

curseforge {
    if(System.getenv('HRZN_CURSE_KEY'))
        apiKey = System.getenv('HRZN_CURSE_KEY')
    else
        apiKey = "NONE"
    project {
        apiUrl="https://minecraft.curseforge.com"
        id = '557428'
        changelog = file('CHANGELOG.md')
        releaseType = 'release'
        mainArtifact jar
        addArtifact apiJar
        addGameVersion "$mc_version"
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://$buildDir/repo")

            pom {
                groupId = project.group
                version = project.version
                artifactId = 'matteroverdrive-legacy-edition'
                project {
                    name "Matter Overdrive: Legacy Edition"
                    packaging 'jar'
                    description 'Legacy Edition of Matter Overdrive'
                    url 'https://hrznstudio.com/mo'
                    developers {
                        developer {
                            id 'coded'
                            name 'Coded'
                            roles {role 'developer'}
                        }
                    }
                }
            }
        }
    }
}